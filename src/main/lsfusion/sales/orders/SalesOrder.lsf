MODULE SalesOrder;

REQUIRE Time, MetaNumerator, Employee, Company, DateUtils,
        MetaTax, TaxItem, BarCode, LocationEmployee, Department,
        SalesSettings, PartnerSales, ItemSales, SalesOrderType,
        PaymentTermsSales, SalesPricelistValue, Doc;  

NAMESPACE Sales;

CLASS Order 'Заказ покупателя';

@defineDocStatus(order, 'заказа покупателя');

// type
type 'Тип' = DATA OrderType (Order) NONULL;
nameType 'Тип' (Order s) = name(type(s));

WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(type(o)) DO type(o) <- defaultOrderType(); 

dateTime 'Дата' = DATA DATETIME (Order);
dateTime(Order o) <- currentDateTime() WHEN SET(o IS Order);
@defineDateTimeAggregation(Order);

@defineNumberType(order);

numberDate 'Описание' (Order o) = number(o) + ' от ' + dateTime(o) IN id;

// customer
customer 'Покупатель' = DATA Partner (Order) NONULL;
nameCustomer 'Покупатель' (Order o) = name(customer(o));
addressCsvCustomer 'Адрес покупателя' (Order o) = addressCsv(customer(o));

CONSTRAINT customer(Order o) AND NOT isCustomer(customer(o))
                CHECKED BY customer[Order]
                MESSAGE 'Контрагент заказа покупателя должен быть покупателем';

@defineDocDepartment(order, customer);

// Quotation
validity 'Срок действия' = DATA DATETIME (Order);

// Invoicing information
paymentTerms = DATA PaymentTerm (Order);
namePaymentTerms 'Условия оплаты' (Order o)= name(paymentTerms(o));

WHEN LOCAL CHANGED(customer(Order o)) AND NOT CHANGED(paymentTerms(o)) DO paymentTerms(o) <- customerPaymentTerms(customer(o)); 

// company
company 'Компания' = DATA Company (Order) NONULL;
nameCompany 'Компания' (Order o) = name(company(o));
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(company(o)) DO company(o) <- defaultCompany();

// Shipping information
scheduledDateTime 'Планируемая дата' = DATA DATETIME (Order);
scheduledDateTime(Order o) <- currentDateTime() WHEN SET(o IS Order);

scheduledDate 'Дата поставки' (Order o) = DATE(scheduledDateTime(o));

deliveryAddress 'Адрес доставки' = DATA STRING (Order) CHARWIDTH 20;
WHEN LOCAL (CHANGED(customer(Order o)) OR CHANGED(department(o))) AND NOT CHANGED(deliveryAddress(o)) DO 
    deliveryAddress(o) <- OVERRIDE addressCsv(department(o)), addressCsvCustomer(o);

// location
location = DATA Location (Order) PREREAD;
nameLocation 'Место хранения' (Order o) = name(location(o));

// currency
currency = DATA Currency (Order);
nameCurrency 'Валюта' (Order o) = name(currency(o));

WHEN LOCAL SETCHANGED(type(Order o)) AND currency(type(o)) AND NOT CHANGED(currency(o)) DO currency(o) <- currency(type(o));  

// Sales information
representative 'Наш представитель' = DATA Employee (Order);
nameRepresentative 'Наш представитель' (Order o)= MasterData.name(representative(o));

representative(Order o) <- currentUser() IF currentUser() IS Employee WHEN SET(o IS Order);

CONSTRAINT representative(Order o) AND NOT legalEntity(representative(o)) = company(o) 
    CHECKED BY representative MESSAGE 'Должен быть наш работник';

customerReference 'Код покупателя' = DATA STRING[30] (Order);

// Lines
CLASS OrderLine 'Строка заказа';
order =  DATA Order (OrderLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER OrderLine l BY order(l) IN id MATERIALIZED CHARWIDTH 3;

item = DATA Item (OrderLine) NONULL;
nameItem '{Item}' (OrderLine l) = name(item(l));
idBarCodeItem 'Штрихкод' (OrderLine l) = idBarCode(item(l));
idItem 'Код' (OrderLine l) = id(item(l));
isService 'Услуга' (OrderLine l) = item(l) IS Service;

CONSTRAINT item(OrderLine l) AND NOT canBeSold(item(l))
            CHECKED BY item[OrderLine] 
            MESSAGE 'Номенклатура не предназначена для продажи';

description 'Описание' = DATA ISTRING (OrderLine) CHARWIDTH 20;

uom (OrderLine l) = uom(item(l));
nameUom 'Ед. изм.' (OrderLine l) = name(uom(l));

quantity '{Quantity}' = DATA NUMERIC[16,3] (OrderLine);

price 'Цена' = DATA NUMERIC[10,2] (OrderLine);

defaultPrice = ABSTRACT CASE NUMERIC[10,2] (OrderLine);
WHEN LOCAL SETCHANGED(item(OrderLine l)) AND NOT CHANGED(price(l)) DO price(l) <- defaultPrice(l);

defaultPrice (OrderLine l) += WHEN salesPrice(item(l)) THEN salesPrice(item(l)); 

@defineDocDiscount(order);

@defineTaxCalc(order, o, sales, resultPrice);
taxIncluded (OrderLine l) += taxIncluded(type(order(l)));

WHEN LOCAL CHANGED(item(OrderLine ol)) DO {
    in(ol, Tax t) <- salesIn(item(ol), t); 
}

// Line properties
nameStatus 'Статус' (OrderLine l) = nameStatus(order(l));
type (OrderLine l) = type(order(l)); 
dateTime 'Дата' (OrderLine l) = dateTime(order(l));
date 'Дата' (OrderLine l) = date(order(l));
number 'Номер' (OrderLine l) = number(order(l));
numberDate 'Описание' (OrderLine l) = numberDate(order(l));
nameLocation 'Место хранения' (OrderLine l) = nameLocation(order(l));

customer (OrderLine l) = customer(order(l));
nameCustomer 'Заказчик' (OrderLine l) = nameCustomer(order(l));

currency (OrderLine l) = currency(order(l));
nameCurrency 'Валюта' (OrderLine l) = nameCurrency(order(l));

FORM order 'Заказ покупателя'
    OBJECTS o = Order PANEL
    PROPERTIES(o) nameType, dateTime, number, 
                  nameCustomer, nameDepartment SHOWIF countDepartment(customer(o)), 
                  nameCompany, nameLocation, nameCurrency,
                  validity,
                  namePaymentTerms, addressCsvCustomer,
                  scheduledDateTime, deliveryAddress, 
                  nameRepresentative, customerReference
    PROPERTIES(o) READONLY untaxedAmount, taxAmount, amount
                 
    OBJECTS l = OrderLine
    PROPERTIES(l) index, nameItem, description, nameUom, idBarCodeItem, idItem, quantity, price, untaxedAmount, taxes ON CHANGE changeTax(l)
    PROPERTIES(l) NEW, DELETE
    FILTERS order(l) = o
    
    EDIT Order OBJECT o    
;      

DESIGN order {
    caption = (CONCAT ' ', 'Заказ покупателя', '№' + number(o), 'от ' + dateTime(o));
    OBJECTS {
        NEW header {
            alignment = STRETCH;   
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(o)) { notNull = TRUE; };
                MOVE PROPERTY(dateTime(o));
                MOVE PROPERTY(number(o));
            }
            NEW headerRight {
                MOVE PROPERTY(nameCustomer(o)) { notNull = TRUE; } 
                MOVE PROPERTY(nameDepartment(o)); 
                MOVE PROPERTY(nameCompany(o)) { notNull = TRUE; }
                MOVE PROPERTY(nameLocation(o));               
                MOVE PROPERTY(nameCurrency(o));
            }    
            NEW relatedDoc {
                type = TABBED;
                fill = 1;
            }                                 
        }
        NEW details {
            fill = 7;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l) {
                    PROPERTY(nameItem(l)) { notNull = TRUE; }
                }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW quotation {
                    caption = 'Запрос предложений';
                    alignment = STRETCH;
                    MOVE PROPERTY(validity(o));
                }
                NEW invoicing {
                    caption = 'Расчеты';
                    alignment = STRETCH;
                    MOVE PROPERTY(namePaymentTerms(o));                         
                    MOVE PROPERTY(addressCsvCustomer(o));
                }
                NEW shippingInformation {
                    caption = 'Поставка';
                    alignment = STRETCH;
                    MOVE PROPERTY(scheduledDateTime(o));
                    MOVE PROPERTY(deliveryAddress(o));
                }
                NEW salesInformation {
                    caption = 'Прочие';
                    alignment = STRETCH;
                    MOVE PROPERTY(nameRepresentative(o));                                       
                    MOVE PROPERTY(customerReference(o));               
                }
            }            
        }
        NEW footer {
            align = END;
            type = CONTAINERH; 
            NEW total {
                caption = 'Итого';
                type = CONTAINERH;
                MOVE PROPERTY(untaxedAmount(o));
                MOVE PROPERTY(taxAmount(o));
                MOVE PROPERTY(amount(o));                     
            }
        }
    }            
}

@defineDocHistory(order, o, item, quantity);
@defineDocDiscountForm(order);

background = ABSTRACT CASE COLOR (Order);
FORM orders 'Заказы покупателей'
     OBJECTS o = Order BACKGROUND background(o)
     PROPERTIES(o) READONLYIF isReadonly()
                            nameType, dateTime, number, nameCustomer, nameDepartment,
                            validity,
                            namePaymentTerms, addressCsvCustomer,
                            scheduledDateTime, deliveryAddress, nameLocation, 
                            nameRepresentative, customerReference,
                            untaxedAmount, taxAmount, amount
                            
     PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE 
;

@extendFormEditable(orders);

@defineDocObjectsForm(orders, o, 'Заказы покупателей');
@defineDocStatusForm(order, o);
@defineDocLinesCount(order, o);
@defineDocLocationAccess(order, o);

NAVIGATOR {
    operations {
        NEW orders FIRST;    
    }
}

clone (Order to, Order from) {
    type(to) <- type(from);
    customer(to) <- customer(from);
    paymentTerms(to) <- paymentTerms(from);
    company(to) <- company(from);
    location(to) <- location(from);
    currency(to) <- currency(from);
    representative(to) <- representative(from); 
    FOR order(OrderLine l) = from INLINE NEW nl = OrderLine DO {
        order(nl) <- to;
        item(nl) <- item(l);
        description(nl) <- description(l);
        
        quantity(nl) <- quantity(l);
        
        price(nl) <- price(l);
    }
}

copy 'Копировать' (Order from)  { 
    NEWSESSION {
        NEW to = Order {
            clone(to, from);
            SHOW order OBJECTS o = to DOCKED;
        }
    }
}

EXTEND FORM order PROPERTIES copy(o);

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(copy(o));
    }
}